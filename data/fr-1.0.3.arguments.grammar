#include fr-1.0.3.sentence.grammar

/* **************************************************
 * Actants
 * agent
 * patient
 * comp
 * recipient
 * source
 ************************************************** */
// il pleut
argumentClause → sentence {
  [i:NIL, ii:NIL, iii:NIL, iv:NIL, $rest];
  [vform:tensed, diathesis:active] ⊂ ↑;
  ↓1 = [subj:[], iiReal:none, $rest];
  ⇑ = ⇓1;
}

// il pleut des grenouilles
// without subject:
argumentClause → sentence {
  [i:NIL, ii:$II, iii:NIL, iv:NIL, $Rest];
  [diathesis:active] ⊂ ↑;
  ↓1 = [subj:[], obj:$II, iiReal:obj, voice:active, $Rest];
  [subj:$iSynt, obj:$iiSynt, $RestSynt] ⊂ ⇓1;
  ⇑ = [i:$iSynt, ii:$iiSynt, $RestSynt];
}

// courir: X
argumentClause → sentence {
  [i:$I, ii:NIL, iii:NIL, iv:NIL, $Rest];
  [diathesis:active] ⊂ ↑;
  ↓1 = [subj:$I, iiReal:none, $Rest];
  [subj:$iSynt, $RestSynt] ⊂ ⇓1;
  ⇑ = [i:$iSynt, $RestSynt];
}

// manger: X Y
argumentClause → sentence {
  [i:$I, ii:$II, iii:NIL, iv:NIL, $Rest];
  [diathesis:active] ⊂ ↑;
  ↓1 = [subj:$I, obj:$II, iiReal:obj, voice:active, $Rest];
  [subj:$iSynt, obj:$iiSynt, $RestSynt] ⊂ ⇓1;
  ⇑ = [i:$iSynt, ii:$iiSynt, $RestSynt];
  //⇑ = ⇓1;
}

// Transitif voix passive
argumentClause → sentence {
  [i:$I, ii:$II, iii:NIL, iv:NIL, diathesis:passive, $Rest];
  $I = $I U [pcas:par];
  ↓1 = [subj:$II, prep_obj:$I, voice:passive, $Rest];
  [prep_obj:$iSynt, subj:$iiSynt, $RestSynt] ⊂ ⇓1;
  ⇑ = [i:$iSynt, ii:$iiSynt, $RestSynt];
}

// provenir : X de Y
argumentClause → sentence {
  [i:$I, ii:$II, iii:NIL, iv:NIL, $Rest];
  ↓1 = [subj:$I, prep_obj:$II, iiReal:prep_obj, voice:active, $Rest];
  [subj:$subjSynt, prep_obj:$objSynt, $RestSynt] ⊂ ⇓1;
  [subj:$iSynt, prep_obj:$iiSynt, $RestSynt] ⊂ ⇓1;
  ⇑ = [i:$iSynt, ii:$iiSynt, $RestSynt];
}

// Transitif voix moyenne: la branche casse
argumentClause → sentence {
  [i:$I, ii:NIL, iii:NIL, iv:NIL, diathesis:middle, $Rest];
  ↓1 = [subj:$I, voice:middle, resObj:yes, $Rest];
  [subj:$iSynt, $RestSynt] ⊂ ⇓1;
  ⇑ = [i:$iSynt, $RestSynt];
}

// s'alimenter: X avec Z
argumentClause → sentence {
  [i:$I, ii:NIL, iii:$III, iv:NIL, $Rest];
  [diathesis:active] ⊂ ↑;
  ↓1 = [subj:$I, prep_obj:$III, iiiReal:prep_obj, $Rest];
  [subj:$iSynt, prep_obj:$iiiSynt, $RestSynt] ⊂ ⇓1;
  ⇑ = [i:$iSynt, iii:$iiiSynt, $RestSynt];
}

// X livre Y à Z
// X coupe Y avec Z
argumentClause → sentence {
  [i:$I, ii:$II, iii:$III, iv:NIL, $Rest];
  [diathesis:active] ⊂ ↑;
  ↓1 = [subj:$I, obj:$II, prep_obj:$III, iiReal:obj, iiiReal:prep_obj, voice:active, $Rest];
  [subj:$iSynt, obj:$iiSynt, prep_obj:$iiiSynt, $RestSynt] ⊂ ⇓1;
  ⇑ = [i:$iSynt, ii:$iiSynt, iii:$iiiSynt, $RestSynt];
}

// Y est livré à Z par X
// Y est coupé avec Z par X
argumentClause → sentence {
  [i:$I, ii:$II, iii:$III, iv:NIL, diathesis:passive, $Rest];
  $I = $I U [pcas:par];
  ↓1 = [subj:$II, prep_obj2:$I, prep_obj:$III, iiReal:obj, iiiReal:prep_obj, voice:passive, $Rest];
  [subj:$iiSynt, prep_obj2:$iSynt, prep_obj:$iiiSynt, $RestSynt] ⊂ ⇓1;
  ⇑ = [i:$iSynt, ii:$iiSynt, iii:$iiiSynt, $RestSynt];
}

// transporter: X Y de Z à K
argumentClause → sentence {
  [i:$I, ii:$II, iii:$III, iv:$IV, $Rest];
  [diathesis:active] ⊂ ↑;
  ↓1 = [subj:$I, obj:$II, prep_obj:$III, prep_obj2:$IV, iiReal:obj, iiiReal:prep_obj, ivReal:prep_obj, $Rest];
  [subj:$iSynt, obj:$iiSynt, prep_obj:$iiiSynt, prep_obj2:$ivSynt, $RestSynt] ⊂ ⇓1;
  ⇑ = [i:$iSynt, ii:$iiSynt, iii:$iiiSynt, iv:$ivSynt, $RestSynt];
}

// vcomp|scomp
/* **************************************************
 * Coréférence
 ************************************************** */
// Controller subject
// Jean souhaite partir
// Jean accepte/refuse de partir
argumentClause → sentence {
  [i:[id:$ID], ii:[i:[idref:$ID]], iii:NIL];
  [i:$I, ii:[i:_, $RestComp], $Rest] ⊂ ↑;
  ↓1 = [subj:$I, vcomp:[real:vcomp, $RestComp], ctr:subj, iiReal:vcomp, $Rest];
  [subj:$iSynt, vcomp:$iiSynt, $RestSynt] ⊂ ⇓1;
  ⇑ = [i:$iSynt, ii:$iiSynt, $RestSynt];
}

// Jean souhaite qu'il parte
// Jean accepte/refuse qu'il parte
argumentClause → sentence {
  [i:[id:$ID], ii:[i:[idref:NIL]], iii:NIL];
  [i:$I, ii:[i:$II_I, $RestComp], $Rest] ⊂ ↑;
  ↓1 = [subj:$I, scomp:[i:$II_I, real:scomp, $RestComp], iiReal:scomp, $Rest];
  [subj:$iSynt, scomp:$iiSynt, $RestSynt] ⊂ ⇓1;
  ⇑ = [i:$iSynt, ii:$iiSynt, $RestSynt];
}

// Jean souhaite qu'il parte
// Jean accepte/refuse qu'il parte
argumentClause → sentence {
  [i:[id:NIL], ii:[i:[idref:NIL]], iii:NIL];
  [i:$I, ii:[i:$II_I, $RestComp], $Rest] ⊂ ↑;
  ↓1 = [subj:$I, scomp:[i:$II_I, real:scomp, $RestComp], iiReal:scomp, $Rest];
  [subj:$iSynt, scomp:$iiSynt, $RestSynt] ⊂ ⇓1;
  ⇑ = [i:$iSynt, ii:$iiSynt, $RestSynt];
}

// Controller à_obj
// La police demande à Marie de circuler
argumentClause → sentence {
  [ii:[i:[idref:$ID1]], iii:[id:$ID1]];
  //attest $ID1 != $ID2;
  [i:$I, ii:[i:_, $RestComp], iii:[id:_, $III], $Rest] ⊂ ↑;
  ↓1 = [subj:$I, vcomp:[real:vcomp, $RestComp], ctr:obj, iiReal:vcomp, prep_obj:$III, iiiReal:prep_obj, $Rest];
  [subj:$iSynt, vcomp:$iiSynt, $RestSynt] ⊂ ⇓1;
  ⇑ = [i:$iSynt, ii:$iiSynt, $RestSynt];
}

// Jean demande à Marie qu'il parte
argumentClause → sentence {
  [i:[id:$ID], ii:[i:[idref:$ID]], iii:[idref:NIL]];
  [i:$I, ii:[i:_, $RestComp], iii:[id:_, $III], $Rest] ⊂ ↑;
  [subj:[number:$N, gender:$G, person:$P], scomp:[i:[number:$N, gender:$G, person:$P]]] ⊂ ⇓1;
  ↓1 = [subj:$I, scomp:[i:[PRED:_pro], real:scomp, $RestComp], iiReal:icomp|scomp, prep_obj:$III, iiiReal:prep_obj, $Rest];
  [subj:$iSynt, scomp:$iiSynt, $RestSynt] ⊂ ⇓1;
  ⇑ = [i:$iSynt, ii:$iiSynt, $RestSynt];
}

// Jean demande à la police s'il peut partir
// Jean demande à Marie qu'elle parte
argumentClause → sentence {
  [ii:[i:[idref:NIL]], iii:[idref:NIL]];
  [i:$I, ii:$II, iii:$III, $Rest] ⊂ ↑;
  ↓1 = [subj:$I, scomp:[real:scomp, $II], iiReal:scomp, prep_obj:$III, iiiReal:prep_obj, $Rest];
  [subj:$iSynt, scomp:$iiSynt, prep_obj:$iiiSynt, $RestSynt] ⊂ ⇓1;
  ⇑ = [i:$iSynt, ii:$iiSynt, iii:$iiiSynt, $RestSynt];
}
